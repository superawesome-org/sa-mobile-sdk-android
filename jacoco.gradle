apply plugin: 'jacoco'

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Jacoco test report
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = file("$buildDir/reports")
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn "testDebugUnitTest"
    group = "Reporting"

    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            'android/**/*.*',
            '**/*Fragment.*',
            '**/*Activity.*'
    ]
    def intermediateExcludes = fileTree(dir: "$buildDir/intermediates/classes/debug", excludes: excludes)
    def kotlinExcludes = fileTree(dir: "$buildDir/tmp/kotlin-classes/debug", excludes: excludes)

    getClassDirectories().setFrom(intermediateExcludes + kotlinExcludes)
    getSourceDirectories().setFrom(files([android.sourceSets.main.java.srcDirs, "src/main/java"]))
    getExecutionData().setFrom(files("$buildDir/jacoco/testDebugUnitTest.exec"))
}

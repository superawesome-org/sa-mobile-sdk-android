apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = properties.getProperty("project.version")
group = properties.getProperty("project.group")

def siteUrl = 'https://developers.superawesome.tv/extdocs/sa-mobile-sdk-android/html/index.html'        // Homepage URL of the library
def gitUrl = 'https://github.com/SuperAwesomeLTD/sa-mobile-sdk-android/tree/master'         // Git repository URL


android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    resourcePrefix "superawesome__"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SuperAwesome-Base"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SADefaults Base Mobile SDK for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'gabrielcoman'
                        name 'Gabriel Coman'
                        email 'gabriel.coman@superawesome.tv'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

repositories {
    maven {
        url "http://dl.bintray.com/gabrielcoman/maven"
    }
    maven {
        url "http://dl.bintray.com/superawesome/SuperAwesomeSDK"
    }
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'tv.superawesome.lib.sautils:sautils:1.0.2'
    compile 'tv.superawesome.lib.saevents:saevents:1.5.5'
    compile 'tv.superawesome.lib.savideoplayer:savideoplayer:1.0.1'
    compile 'tv.superawesome.lib.sawebplayer:sawebplayer:1.1.0'
    compile 'tv.superawesome.lib.saadloader:saadloader:1.5.4'
    compile 'tv.superawesome.lib.saparentalgate:saparentalgate:1.0.3'
    compile 'tv.superawesome.lib.sabumperpage:sabumperpage:1.0.6'
    compile 'tv.superawesome.sagdprisminorsdk:sagdprisminorsdk:2.1.0'
}

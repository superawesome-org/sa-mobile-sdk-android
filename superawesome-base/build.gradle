apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = properties.getProperty("project.version")
group = properties.getProperty("project.group")

def siteUrl = 'https://developers.superawesome.tv/extdocs/sa-mobile-sdk-android/html/index.html'        // Homepage URL of the library
def gitUrl = 'https://github.com/SuperAwesomeLTD/sa-mobile-sdk-android/tree/master'         // Git repository URL


android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    resourcePrefix "superawesome__"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    testOptions {
        animationsDisabled true
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SuperAwesome-Base"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SADefaults Base Mobile SDK for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'gabrielcoman'
                        name 'Gabriel Coman'
                        email 'gabriel.coman@superawesome.tv'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

repositories {
    maven {
        url "http://dl.bintray.com/gabrielcoman/maven"
    }
    maven {
        url "http://dl.bintray.com/superawesome/SuperAwesomeSDK"
    }
    mavenCentral()
}

dependencies {
    compile "tv.superawesome.lib.savideoplayer:savideoplayer:2.0.0-beta6"
    compile files('libs/moatlib.jar')
    compile files('libs/json.jar')

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // Tests
    ////////////////////////////////////////////////////////////////////////////////////////////////

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.+"
    testImplementation "eu.codearte.catch-exception:catch-exception:1.4.4"
    testImplementation "org.assertj:assertj-core:3.8.0"
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation 'org.skyscreamer:jsonassert:1.3.0'

    testImplementation files('libs/json.jar')

    def testRunnerVersion = "1.0.2"
    def testRulesVersion = "1.0.2"
    def espressoCoreVersion = "3.0.2"
    def espressoIdlingVersion = "3.0.2"
    def espressoContribVersion = "2.2.2"
    def okhttpVersion = "3.9.1"

    // A Mockito wrapper enabling more powerful & verbose tests
    androidTestImplementation "com.android.support.test:runner:$testRunnerVersion"
    androidTestImplementation "com.android.support.test:rules:$testRulesVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoCoreVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$espressoIdlingVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoContribVersion"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = properties.getProperty("project.version")
group = properties.getProperty("project.group")

def siteUrl = 'https://developers.superawesome.tv/extdocs/sa-mobile-sdk-android/html/index.html'        // Homepage URL of the library
def gitUrl = 'https://github.com/SuperAwesomeLTD/sa-mobile-sdk-android/tree/master'         // Git repository URL


android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    resourcePrefix "superawesome__"

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName version
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    testOptions {
        animationsDisabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SuperAwesome-Base"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SADefaults Base Mobile SDK for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'gabrielcoman'
                        name 'Gabriel Coman'
                        email 'gabriel.coman@superawesome.tv'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

repositories {
    maven { url "http://dl.bintray.com/gabrielcoman/maven" }
    maven { url "http://dl.bintray.com/superawesome/SuperAwesomeSDK" }
    mavenCentral()
}

dependencies {
    implementation "tv.superawesome.lib.savideoplayer:savideoplayer:2.0.0-beta6"
    implementation files('libs/moatlib.jar')
    implementation files('libs/json.jar')

    // Tests
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation "eu.codearte.catch-exception:catch-exception:2.0"
    testImplementation "org.assertj:assertj-core:3.16.1"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.6.0'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.6.0"
}

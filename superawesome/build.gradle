import groovy.json.JsonSlurper

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName getSdkVersion()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    publishDescription = 'SuperAwesome Full SDK'
}

repositories {
    maven { url "https://dl.bintray.com/superawesome/SuperAwesomeSDK" }
}

dependencies {
    api project(':superawesome-common')
}
def parsedJson = new JsonSlurper().parseText(file("credentials.json") .text)

afterEvaluate {
    publishing {
        publications {
            SuperAwesome(MavenPublication) { // MODULE NAME HERE
                groupId = group
                artifactId = it.name
                version = it.version

                from components.release

                // UPDATE THE META DATA HERE
                pom {
                    name = 'SuperAwesome'
                    description = 'The android library for the AwesomeAds.'
                    url = 'https://github.com/SuperAwesomeLTD/sa-mobile-sdk-android.git'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'gabrielcoman'
                            name = 'Gabriel Coman'
                            email = 'gabriel.coman@superawesome.com'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url "s3://prod-rukkaz-cms/android_repo/"
                credentials(AwsCredentials) {
                    accessKey parsedJson.get("SA_ANDROID_APPS_S3_REPO_KEY")
                    secretKey parsedJson.get("SA_ANDROID_APPS_S3_REPO_SECRET")
                }
            }
        }
    }
}